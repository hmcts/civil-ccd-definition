plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.14.RELEASE'
  id 'org.owasp.dependencycheck' version '7.2.1'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.sonarqube' version '3.4.0.2513'
  id 'au.com.dius.pact' version '4.2.7'
  id 'net.ltgt.apt' version '0.21'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

def versions = [
  junit              : '5.7.0',
  junitPlatform      : '1.9.1',
  reformLogging      : '5.1.5',
  jackson            : '2.14.0-rc1'
]

allprojects {

  sourceCompatibility = '11'
  targetCompatibility = '11'

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'org.owasp.dependencycheck'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'net.ltgt.apt'

  checkstyle {
    maxWarnings = 0
    toolVersion = '8.29'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  jacoco {
    toolVersion = '0.8.5' // jacocoMavenPluginVersion
    reportsDir = file("$buildDir/reports/jacoco")
  }

// before committing a change, make sure task still works
  dependencyUpdates {
    def isNonStable = { String version ->
      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
      def regex = /^[0-9,.v-]+$/
      return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
      return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
  }

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }
  dependencyManagement {
    dependencies {
      dependencySet(
        group: 'com.fasterxml.jackson.core',
        version: versions.jackson
      ) {
        entry 'jackson-core'
      }
      dependencySet(
        group: 'com.fasterxml.jackson.core',
        version: versions.jackson
      ) {
        entry 'jackson-databind'
      }
      dependencySet(
        group: 'com.fasterxml.jackson.datatype',
        version: versions.jackson
      ) {
        entry 'jackson-datatype-jdk8'
        entry 'jackson-datatype-jsr310'
      }
      dependencySet(
        group: 'com.fasterxml.jackson.module',
        version: versions.jackson
      ) {
        entry 'jackson-module-parameter-names'
      }
      dependencySet(
        group: 'com.fasterxml.jackson.core',
        version: versions.jackson
      ) {
        entry 'jackson-annotations'
      }
      // solves CVE-2022-25857
      dependencySet(
        group: 'org.yaml',
        version: '1.33'
      ) {
        entry 'snakeyaml'
      }
    }
  }
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
      url 'https://jitpack.io'
    }
    maven {
      url  "http://repo.spring.io/milestone"
    }
    maven {
      url  "https://repo.spring.io/libs-milestone"
    }
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

jar {
  getArchiveFileName().set(provider {
    'empty.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

task highLevelDataSetup(type: JavaExec) {
  main = "uk.gov.hmcts.reform.civil.HighLevelDataSetupApp"
  classpath += sourceSets.main.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task installDependencies(type: Exec, description: 'Installs Yarn dependencies.') {
  commandLine '/usr/bin/yarn', '--mutex', 'network', '--frozen-lockfile', '--silent', 'install'
}

task checkDependenciesIntegrity(type: Exec, description: 'Checks integrity of Yarn dependencies.') {
  commandLine '/usr/bin/yarn', '--mutex', 'network', '--frozen-lockfile', '--silent', 'check', '--integrity'
}

task lintUserInterfaceTests(type: Exec, description: 'Runs linting of E2E tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'lint'
}

task awaitApplicationReadiness(type: Exec, description: 'Awaits until application is ready.') {
  commandLine './bin/wait-for.sh', System.env.URL
  commandLine './bin/wait-for.sh', System.env.TEST_URL
}

task runSmokeTests(type: Exec, description: 'Runs smoke tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'test:smoke'
}

task runSpecFunctionalTests(type: Exec, description: 'Runs Spec functional tests.') {
  onlyIf {
    return System.env.ENVIRONMENT == 'preview'
  }
  commandLine '/usr/bin/yarn', '--silent', 'test:e2e-spec'
}

task runSdoFunctionalTests(type: Exec, description: 'Runs Spec functional tests.') {
  onlyIf {
    return System.env.ENVIRONMENT == 'preview'
  }
  commandLine '/usr/bin/yarn', '--silent', 'test:e2e-sdo'
}

task runUnspecFunctionalTests(type: Exec, description: 'Runs Unspec functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'test:functional-unspec'
}

task runDJFunctionalTests(type: Exec, description: 'Runs default judgement functional tests.') {
 onlyIf {
    return System.env.ENVIRONMENT == 'preview'
  }
  commandLine '/usr/bin/yarn', '--silent', 'test:e2e-dj'
}

task demoEnvNonProdFeaturesEnabledFunctionalTests(type: Exec, description: 'Runs Non prod functional tests in demo') {
  commandLine '/usr/bin/yarn', '--silent', 'test:DemoFunctional'
}

task runRpaHandOffTests(type: Exec, description: 'Runs functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'test:rpa'
}

task runNightlyMultipartyTests(type: Exec, description: 'Runs additional multiparty nightly tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'test:nightly-multiparty'
}

def inStrictOrder(Task... tasks) {
  for (int i = 0; i < tasks.size() - 1; i++) {
    tasks[i + 1].mustRunAfter(tasks[i])
  }
  return tasks
}

task smoke(description: 'Runs the smoke tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness, installDependencies, checkDependenciesIntegrity, lintUserInterfaceTests, runSmokeTests))
}

task functional(description: 'Runs the functional tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness, installDependencies, checkDependenciesIntegrity, runSdoFunctionalTests, runSpecFunctionalTests, runUnspecFunctionalTests, runDJFunctionalTests))
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "CIVIL :: ccd-definition"
    property "sonar.projectKey", "civil-ccd-definition"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.coverage.exclusions", "**/HighLevelDataSetupApp.java"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
  }
}

jacocoTestReport {
  executionData(test)
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
  subprojects.each {
    sourceSets it.sourceSets.main
  }
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

jacocoTestReport.dependsOn {
  subprojects*.test
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'org.springframework', name: 'spring-core', version: '5.3.23'
  implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.23'
  implementation group: 'org.apache.poi', name: 'poi', version: '5.2.2'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'
  implementation group: 'com.github.hmcts', name: 'befta-fw', version: '8.7.1'

  constraints {
    implementation('org.apache.commons:commons-compress:1.21') {
      because 'CVE-2021-35515, CVE-2021-35516, CVE-2021-35517, CVE-2021-36090'
    }
  }


  testImplementation libraries.junit5

  functionalTestImplementation sourceSets.main.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath

  // prevent CVE-2020-0187
  implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
  // prevent CVE-2022-22978
  implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.7.2'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}
